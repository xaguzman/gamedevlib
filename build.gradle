buildscript{
    repositories{
        mavenCentral()
        mavenLocal()
    }
}

allprojects{
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    repositories{
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        mavenLocal()
    }
}

ext {
    projectVersion = '0.0.1'
    projectGroupId = 'com.github.xaguzman'
    gdxVersion = '1.9.4'
    artemisVersion = '2.0.0'
    distDir = file('dist/')
}


subprojects{
    // Binary Jar
    jar {
        destinationDir      = distDir
    }

    task javadocJar(type: Jar, dependsOn:javadoc){
        classifier = 'javadoc'
        from javadoc, sourceSets.main.allSource
        destinationDir      = distDir
    }

    task sourcesJar(type: Jar, dependsOn:classes){
        classifier = 'sources'
        from sourceSets.main.allSource
        destinationDir      = distDir
    }

    buildDir = rootProject.buildDir
}

project(':libgdx'){

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"

        testCompile 'junit:junit:4.10'
        testCompile "com.badlogicgames.gdx:gdx:$gdxVersion"
        testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        testCompile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    }
}


project(':artemisodb'){

    dependencies {
        compile project(":libgdx")
        compile "net.onedaybeard.artemis:artemis-odb:$artemisVersion"

        testCompile 'junit:junit:4.10'
    }
}

tasks.eclipse.doLast {
    delete ".project"
}


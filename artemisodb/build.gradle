sourceCompatibility = 1.6
sourceSets.main.java.srcDirs = [ "src/main/" ]
sourceSets.test.java.srcDirs = [ "src/tests/" ]
sourceSets.test.resources.srcDirs = [ "src/tests/resources" ]

project.archivesBaseName  = 'gamedevlib-artemisodb'

eclipse.project {
    name = project.archivesBaseName
}

apply plugin: 'maven'
apply plugin: 'signing'

artifacts{
    archives jar, javadocJar, sourcesJar
}

signing {
//    required false
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Gamedevlib - Artemisodb'
                description 'A set of utilities to speed up libgdx game dev (with artemis odb)'
                version project.projectVersion
                groupId project.projectGroupId
                artifactId project.archivesBaseName

                packaging 'jar'
                url 'https://github.com/xaguzman/gamedevlib'

                scm {
                    connection 'scm:git:git://github.com/xaguzman/gamedevlib.git'
                    developerConnection 'scm:git:git@github.com:xaguzman/gamedevlib.git'
                    url 'http://github.com/xaguzman/gamedevlib'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'xaguzman'
                        name 'Xavier Guzman'
                        email 'xaguzman@gmail.com'
                        timezone 'CST  (GMT-6)'
                    }
                }

                dependencies {
                    dependency {
                        groupId project.projectGroupId
                        artifactId 'gamedevlib-libgdx'
                        version project.projectVersion
                        scope 'compile'
                    }
                }
            }
        }
    }
}

task testPom << {
    uploadArchives.repositories.mavenDeployer.pom.writeTo("$buildDir/artemisodb.pom.xml")
    print("created $buildDir/artemisodb.pom.xml")
}

def installer = install.repositories.mavenInstaller
def deployer = uploadArchives.repositories.mavenDeployer
[installer, deployer]*.pom*.whenConfigured { pom ->
    //hacky way to fix the dependency on the core library...
    pom.dependencies = pom.dependencies.findAll { dep -> dep.groupId != 'gamedev-lib' && dep.version != 'unspecified' }
//    print(pom.dependencies)
}

task Creds << {
    println ossrhUsername
    println ossrhPassword
}

uploadArchives.dependsOn += Creds